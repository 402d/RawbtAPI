plugins {
    id 'com.android.library'
    id 'maven-publish'
}

android {
    compileSdkVersion 30

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 30
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

}

dependencies {
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'androidx.annotation:annotation:1.2.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                groupId = 'com.github.402d'
                artifactId = 'RawbtAPI'
                version = '1.0'

                artifact(sourceJar)

                pom {
                    name = 'RawbtAPI'
                    description = 'Library for integration with the printing service (RawBT) for thermal printers.'
                    url = 'https://github.com/402d/RawbtAPI'
                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'https://opensource.org/licenses/mit-license.html'
                        }
                    }
                    developers {
                        developer {
                            id = '402d'
                            name = 'Oleg Muraveyko'
                            email = 'oleg@muraveyko.ru'
                        }
                    }

                }
            }
        }
    }
}

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "sources"
}